<snippet>
  <content><![CDATA[
* ============== [Sigle answer base check Macro]===========================================================.
** 특정 조건(필터조건)에 만족한 경우에만 단수문항에 응답이 있어야 하는 경우                                .
** 혹은 모든 응답자가 응답을 해야 하는 경우                                                                .
* 예1) Ffreq Q2 (1 THRU 9)                                                                                 .
* 예2) Ffreq Q2 (1 THRU 9)      / Q1 = 1                                                                   .
* 예3) Ffreq Q2 (1 THRU 9)      / Q1 = 1    / Q1                                                           .
*     !     단수문항 (보기코드) / 필터 조건 / 오류 있을 때 LIST 로 Q2와 함께 응답 데이타를 보고싶은 문항   .
* ---------------------------------------------------------------------------------------------------------.
DEFINE !SAfreq  (!POS=!TOKENS(1) / !POS=!CHAREND('/') / !POS=!CHAREND('/') / !POS=!CMDEND)
    !LET !ID=RECORD
    COMP  #BaseChk=0.
    STRING ERR (A22).
    !IF (!2 !EQ !NULL) !THEN
      COMP  #codedDataCnt = NVALID(!1).
      !LET !CODE = 'Freecode '
    !ELSE
      COUNT #codedDataCnt = !1 !2.
      !LET !CODE = !CONCAT(' code ',!2)
    !IFEND
    COMP #puncherrCnt  = NVALID(!1).
    COMP #puncherrCnt  = #puncherrCnt-#codedDataCnt.

    !IF (!3 !NE !NULL) !THEN
      IF (!3) #BaseChk=1.
      STRING BASE  (!CONCAT(A,!LENGTH(!CONCAT(!3,'XX')))).
      COMP BASE=!QUOTE(!CONCAT('[',!3,']')).
      !LET !TIT=!CONCAT('[',!1,']',!CODE,'-BASE:',!UPCASE(!3))
    !ELSE
      COMP  #BaseChk=1.
      STRING BASE (A10).
      COMP BASE="[All base]".
      !LET !TIT=!CONCAT('[',!1,']',!CODE,'-BASE:All base')
    !IFEND

    IF (#codedDataCnt=0 AND #BaseChk=1) ERR="Data Missing →".
    IF (#puncherrCnt >0 AND #BaseChk=1) ERR="Punch Error → ".
    IF (#codedDataCnt>0 AND #BaseChk=0) ERR="Logic error →".


    * 오류 확인하기 (LIST).
    TITLE !TIT.
    TEM.
    SEL IF (ERR<>'').
    LIST !ID ERR !1 BASE !4.
    DELETE VARIABLE BASE ERR.
    *----------------------------------------------.
    *  Freq !1.
    *----------------------------------------------.
!ENDDEFINE.
* ---------------------------------------------------------------------------------------------------------.


* ============== [Multi answer base check Macro]===========================================================.
** 특정 조건(필터조건)에 만족한 경우에만 복수문항에 응답이 있어야 하는 경우                                .
** 혹은 모든2 응답자가 응답을 해야 하는 경우                                                               .
* 예) MAfreq Q27_1_1 TO Q27_1_7 (1)                                                                        .
* 예) MAfreq Q27_1_1 TO Q27_1_7 (1)       / Q16_1 = 1                                                      .
* 예) MAfreq Q27_1_1 TO Q27_1_7 (1)       / Q16_1 = 1 / Q16_1                                              .
*    !       복수문항 list   (입력코드)   / 필터 조건 / 오류 있을 때 LIST 로 Q2와 함께 응답 데이타를 보고싶은 문항.
* 결과1) base가 맞지 않는 응답자 list 출력                                                                 .
* 결과2) Freq 출력 (굳이 Freq까지 필요 없는 경우 ★ 에서 * 넣기                                            .
* ---------------------------------------------------------------------------------------------------------.

DEFINE !MAfreq  (!POS=!CHAREND('(') / !POS=!CHAREND(')') / !POS=!CHAREND('/') / !POS=!CHAREND('/') / !POS=!CMDEND)
   !LET !ID=RECORD
   *----------------------------------------------.
      COMP  #codedDataCnt=0.
      COUNT #codedDataCnt = !1 (!2).
      COUNT #puncherrCnt  = !1 (LOW THRU HI).
      COUNT #ZeroCnt      = !1 (0).
      COMP  #puncherrCnt  = #puncherrCnt-#codedDataCnt-#ZeroCnt.
   *----------------------------------------------.
      COMP  #BaseChk=0.
      !LET !Qtit1=!CONCAT('[',!HEAD(!1))
      !LET !Qtit2=!TAIL(!1)
      !DO !VAR !IN (!Qtit2)
        !LET !Qtit3=!VAR
      !DOEND
      !LET !Qtit1=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")
      STRING ERR (A22).
      !IF (!4 !NE !NULL) !THEN
        IF (!4) #BaseChk=1.
        STRING BASE  (!CONCAT(A,!LENGTH(!CONCAT(!4,'XX')))).
        COMP BASE=!QUOTE(!CONCAT('[',!4,']')).
        !LET !TIT=!CONCAT(!Qtit1,' code ',!2,'-BASE:',!UPCASE(!4))
      !ELSE
        COMP  #BaseChk=1.
        STRING BASE (A10).
        COMP BASE="[All base]".
        !LET !TIT=!CONCAT(!Qtit1,' code ',!2,'-BASE:All base')
      !IFEND
      DO IF (#BaseChk=0).
        COUNT #puncherrCnt  = !1 (LOW THRU HI).
      END IF.
      IF (#codedDataCnt=0 AND #BaseChk=1) ERR="Data Missing →".
      IF (#puncherrCnt >0 AND #BaseChk=1) ERR="Punch Error → ".
      IF (#puncherrCnt >0 AND #BaseChk=0) ERR="Logic error →".
      IF (#codedDataCnt>0 AND #BaseChk=0) ERR="Logic error →".

    * 오류 확인하기 (LIST).
      TITLE !TIT.
      TEM.
      SEL IF (ERR<>'').
      LIST !ID ERR !1 BASE !5 .
      DELETE VARIABLE BASE ERR.
      EXECUTE.
   *----------------------------------------------.
   *  MULT RES GROUP=M !Qtit1 (!1 (!2))/Freq M.
   *----------------------------------------------.

!ENDDEFINE.
* ---------------------------------------------------------------------------------------------------------.


* ============== 특정 로직에 대한 에러 ID 찾기 ============================================================.
* 예1) LOGchk1 Q3_1 / ANY(1, Q1 Q2_1 TO Q2_19)                                                             .
*         대상변수/ 만족해야 하는 조건                                                                     .
* 예2) LOGchk1 Q3_1 / ANY(1, Q1 Q2_1 TO Q2_19) / Q3_1=1                                                    .
*         대상변수/ IF                       / 만족해야 하는 조건                                          .
* ---------------------------------------------------------------------------------------------------------.
DEFINE !LOGchk1 (!POS = !CHAREND ('/') / !POS = !CHAREND ('/') / !POS=!CMDEND)
    !LET !ID=RECORD
    STRING ERR (A15).
    COMP    #BaseChk=0.

    !IF (!3 !EQ !NULL) !THEN
      STRING logic (!CONCAT(A,!LENGTH(!CONCAT('[',!1,'] Should be [',!2,']')))).
      COMP logic = !QUOTE(!CONCAT('[',!1,'] Should be [',!2,']')).
      IF (MIS(!1)=0 AND (!2)) #BaseChk=1.
      IF (MIS(!1)=0 AND #BaseChk=0) ERR="Logic error →".
      !LET !TIT = !CONCAT('IF [',!1,'] Should be [',!2,']')
    !ELSE
      STRING logic (!CONCAT(A,!LENGTH(!CONCAT('IF [',!2,'] Should be [',!3,']')))).
      COMP logic = !QUOTE(!CONCAT('IF [',!2,'] Should be [',!3,']')).
      IF ((!2) AND (!3)) #BaseChk=1.
      IF ((!2) AND  #BaseChk=0) ERR="Logic error →".
      !LET !TIT = !CONCAT('IF [',!2,'] Should be [',!3,']')
    !IFEND

    TITLE !TIT.
    * 오류 확인하기 (LIST).
    TEM.
    SEL IF (ERR<>'').
    LIST !ID ERR !1 logic.
    DELETE VARIABLE ERR logic.

!ENDDEFINE.
* ---------------------------------------------------------------------------------------------------------.



* ====== 동일한 보기코드를 가진 단수 & 복수(BASE 문항) 응답 크로스 체크 (예: 최선호, 선호 )================.
*LOGchk2 단수문항 / 복수문항 list(BASE 문항)                                                               .
*LOGchk2  Q2      / Q1_1  Q1_2  Q1_3  Q1_4  Q1_5.                                                          .
*           '1'        '1'                                                                                 .
*           '2'              '1'                                                                           .
*           '3'                    '1'                                                                     .
* 단, 복수문항 List 는 _1 부터 _X 까지 +1씩 순차적이어야 함                                                .
* ---------------------------------------------------------------------------------------------------------.
DEFINE !LOGchk2 (!POS = !CHAREND ('/') / !POS=!CMDEND)
    !LET !ID=RECORD

    !LET !Qtit1=!CONCAT('[',!HEAD(!2))
    !LET !Qtit2=!TAIL(!2)
    !DO !VAR !IN (!Qtit2)
      !LET !Qtit3=!VAR
    !DOEND
    !LET !Qtit1=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")

    STRING ERR (!CONCAT(A,!LENGTH(!CONCAT('[',!1,' ] Logic error')))).
    COMP #CNT=0.
    DO REPEAT ZQ=!2.
      COMP #CNT=#CNT+1.
      !LET  !BaseQ=!SUBSTR(!2,1,!INDEX(!2,"_"))
      IF (!1=#CNT AND (ZQ<>#CNT OR  MIS(ZQ)=1)) ERR=!QUOTE(!CONCAT('[',!BaseQ,'] Not coded')).
    END REPEAT.

    !LET !TIT=!CONCAT('[',!1,' / ',!Qtit1,'] - Masking Logic chk')
    TITLE !TIT.
    TEM.
    SEL IF(ERR<>'').
    LIST !ID  !1 ERR !2.
    DELETE VARIABLES ERR.
!ENDDEFINE.
* ---------------------------------------------------------------------------------------------.


* ====== 동일한 보기코드를 가진 복수 & 복수(BASE 문항) 응답 크로스 체크 (예: 구입,인지 )===================.
*LOGchk3 복수문항 list / 복수문항 list(BASE 문항)                                                          .
*LOGchk3  Q2_1 to Q2_5 / Q1_1 to Q1_5.                                                                     .
* 단, 복수문항 List 는 _1 부터 _X 까지 +1씩 순차적이어야 함                                                .
* ---------------------------------------------------------------------------------------------------------.
DEFINE !LOGchk3 (!POS = !CHAREND ('/') / !POS=!CMDEND)
    !LET !ID=RECORD

    !LET !Qtit1=!CONCAT('[',!HEAD(!1))
    !LET !Qtit2=!TAIL(!1)
    !DO !VAR !IN (!Qtit2)
      !LET !Qtit3=!VAR
    !DOEND
    !LET !Qtita=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")

    !LET !Qtit1=!CONCAT('[',!HEAD(!2))
    !LET !Qtit2=!TAIL(!2)
    !DO !VAR !IN (!Qtit2)
      !LET !Qtit3=!VAR
    !DOEND
    !LET !Qtitb=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")

    STRING ERR (!CONCAT(A,!LENGTH(!CONCAT('[',!1,' ] Logic error')))).
    COMP #CNT=0.
    DO REPEAT ZQ1=!1
             /ZQ2=!2.
      COMP #CNT=#CNT+1.
      !LET  !BaseQ=!SUBSTR(!2,1,!INDEX(!2,"_"))
      IF (ZQ1=#CNT AND (ZQ2<>#CNT OR  MIS(ZQ2)=1)) ERR=!QUOTE(!CONCAT('[',!BaseQ,'] Not coded')).
    END REPEAT.

    !LET !TIT=!CONCAT('[',!Qtita,' / ',!Qtitb,'] - Masking Logic chk')
    TITLE !TIT.
    TEM.
    SEL IF(ERR<>'').
    LIST !ID  !1 ERR !2.
    DELETE VARIABLES ERR.
!ENDDEFINE.
* ---------------------------------------------------------------------------------------------.

DEFINE !LOGchk4 (!POS = !CHAREND ('/') / !POS=!CMDEND)
    !LET !ID=RECORD

    !LET !Qtit1=!CONCAT('[',!HEAD(!1))
    !LET !Qtit2=!TAIL(!1)
    !DO !VAR !IN (!Qtit2)
      !LET !Qtit3=!VAR
    !DOEND
    !LET !Qtita=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")

    !LET !Qtit1=!CONCAT('[',!HEAD(!2))
    !LET !Qtit2=!TAIL(!2)
    !DO !VAR !IN (!Qtit2)
      !LET !Qtit3=!VAR
    !DOEND
    !LET !Qtitb=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")

    STRING ERR (!CONCAT(A,!LENGTH(!CONCAT('[',!1,' ] Logic error')))).
    COMP #CNT=0.
    DO REPEAT ZQ1=!1
             /ZQ2=!2.
      COMP #CNT=#CNT+1.
      !LET  !BaseQ=!SUBSTR(!2,1,!INDEX(!2,"_"))
      IF (MIS(ZQ1)=0 AND (ZQ2<>#CNT OR  MIS(ZQ2)=1)) ERR=!QUOTE(!CONCAT('[',!BaseQ,'] Not coded')).
      IF (MIS(ZQ1)=1 AND ZQ2=#CNT) ERR=!QUOTE(!CONCAT('[',!BaseQ,'] Missing')).
    END REPEAT.

    !LET !TIT=!CONCAT('[',!Qtita,' / ',!Qtitb,'] - Masking Logic chk')
    TITLE !TIT.
    TEM.
    SEL IF(ERR<>'').
    LIST !ID  !1 ERR !2.
    DELETE VARIABLES ERR.
!ENDDEFINE.


* ============== 중복 응답 ID 찾기 (예:순위문항)===========================================================.
* 예1) DUPchk  A1_1st  A1_2nd  A1_3rd                                                                      .
* 예2) DUPchk  A1_1st  A1_2nd  A1_3rd / 9                                                                  .
*                                       Single code (None)                                                 .
* 변수의 개수가 100개를 초과할 경우 Freq 확인 불가                                                         .
* 변수값의 자리수가 4자리를 초과하는 경우 Freq 확인 불가                                                   .
* ---------------------------------------------------------------------------------------------------------.
DEFINE !DUPchk (!POS = !CHAREND ('/') / !POS = !TOKENS(1))
    !LET !ID=RECORD
    STRING ERR (A35).
    !LET !Qtit1=!CONCAT('[',!HEAD(!1))
    !LET !Qtit2=!TAIL(!1)
    !DO !VAR !IN (!Qtit2)
      !LET !Qtit3=!VAR
    !DOEND
    !LET !Qtit1=!CONCAT(!Qtit1,' ~ ', !Qtit3,"]")
    * 중복 제거 전 Freq.
    *-------------------------------------------------------------.
    *    Mult res group=M Before !Qtit1 (!1 (1,9999))/Freq M.
    *-------------------------------------------------------------.
    * VECTOR에 값 넣기 (#J : 총 문항 개수).
    VECTOR  #TEMPVEC (1000,F10).
    COMP #J=0.
    COMP #TEMP1=0.
    COMP #TEMP2=0.
    DO REPEAT RQ=!1.
      COMP #J=#J+1.
      COMP #TEMPVEC(#J)=RQ.
     *Single 코드 확인.
     !IF (!2 !NE !NULL) !THEN
        IF (RQ=!2 AND #TEMP1=0) #TEMP1=#J.
     !IFEND
      IF (MIS(RQ)=0) #TEMP2=#TEMP2+1.
    END REPEAT.
    DO IF (#TEMP1>0 AND #TEMP1<#TEMP2).
         COMP ERR=!QUOTE(!CONCAT('Single code ',!2,'→')).
    END IF.

    * 중복 제거 후 VECTOR에 값 넣기.
      COMP #K=2.
      DO REPEAT RAWQ=!1.
        LOOP #I=#K TO #J.
          DO IF (RAWQ = #TEMPVEC(#I)).
             COMP #TEMPVEC(#I)=\$SYSMIS.
             COMP ERR='Codes should not be Duplicated →'.
          END IF.
        END LOOP.
        COMP #K=#K+1.
      END REPEAT.
    * 중복 제거 후 Freq.
    *-------------------------------------------------------------.
    *    Mult res group=M Before !Qtit1 (!1 (1,9999))/Freq M.
    *-------------------------------------------------------------.
    TITLE !QUOTE(!CONCAT(!1,' - Duplication check')).
    * 오류 확인하기 (LIST).
    TEM.
    SEL IF (ERR<>'').
    LIST !ID ERR !1.
    DELETE VARIABLE ERR.
    EXECUTE.
!ENDDEFINE.
* ---------------------------------------------------------------------------------------------------------.
]]></content>
  <!-- Optional: Set a tabTrigger to define how to trigger the snippet -->
  <tabTrigger>syntax</tabTrigger>
  <!-- Optional: Set a scope to limit where the snippet will trigger -->
  <scope>text.xml</scope>
</snippet>
